prefix prov: <http://www.w3.org/ns/prov#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix neurolex: <http://neurolex.org/wiki/>
prefix spm: <http://www.incf.org/ns/nidash/spm#>
prefix nidm: <http://www.incf.org/ns/nidash/nidm#>
prefix niiri: <http://iri.nidash.org/>
prefix crypto: <http://id.loc.gov/vocabulary/preservation/cryptographicHashFunctions#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?cluster ?peak ?x ?y ?z ?value ?pval ?stat WHERE 
{ ?peak a nidm:Peak . 
  ?cluster a nidm:Cluster . 
  ?peak prov:wasDerivedFrom ?cluster .
  ?peak prov:atLocation [ nidm:coordinate1 ?x ;
                          nidm:coordinate2 ?y ;
                          nidm:coordinate3 ?z ] .
  ?peak prov:value ?value .
  ?peak nidm:qValueFDR ?pval .
  ?cluster prov:wasDerivedFrom/prov:wasGeneratedBy/prov:used ?statmap .
  ?statmap a nidm:StatisticMap . 
  ?statmap nidm:statisticType ?stat .
}
ORDER BY ?cluster ?peak
